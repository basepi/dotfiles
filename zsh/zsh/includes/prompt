# Prompt

function collapse_pwd {
    echo $(pwd | sed -e "s,^$HOME,~,")
}

function prompt_char {
    git branch >/dev/null 2>/dev/null && echo '±' && return
    hg root >/dev/null 2>/dev/null && echo '☿' && return
    echo '○'
}

# Determine active Python virtualenv details.
function virtualenv_info () {
  PYENV_VERSION="`pyenv version-name`"
  PYENV_SHORT="${PYENV_VERSION%%/*}"
  PYENV_VENV="${PYENV_VERSION##*/}"
  if [[ "$PYENV_VERSION" == "system" ]] ; then
      echo ""
  elif [[ "$PYENV_VENV" == "$PYENV_VERSION" ]] ; then
      echo "[${PYENV_VERSION}] "
  else
      echo "[${PYENV_SHORT}/${PYENV_VENV}] "
  fi
}


local d="%{$FX[reset]$FG[025]%}"
local name="%{$FX[reset]$FG[133]%}%n"
local host="%{$FX[reset]$FG[136]%}%m"
local green="%{$FX[reset]$FG[070]%}"
local white="%{$FG[012]%}"
local tstamp="%{$FG[037]%}%*"

ZSH_GIT_PROMPT_PREFIX=" [%{$FX[reset]$FG[133]%}"
ZSH_GIT_PROMPT_SUFFIX="%{$FX[reset]$FG[025]%}]"
ZSH_GIT_PROMPT_DIRTY="%{$FX[reset]$FG[070]%}?"
ZSH_GIT_PROMPT_UNTRACKED="%{$FX[reset]$FG[070]%}?"
ZSH_GIT_PROMPT_CLEAN=""

ZSH_VIM_PROMPT=" ${d}[${green}NORMAL${d}]"

# Set it initially
PROMPT='
${d}[${name}${d}@${host}${d}] [${green}$(collapse_pwd)${d}]$(git_prompt_info) ${${KEYMAP/vicmd/$ZSH_VIM_PROMPT}/(main|viins)/}
${d}$(virtualenv_info)$(prompt_char) ${white}'

function zle-line-init zle-keymap-select {
    PROMPT='
${d}[${tstamp}${d}] [${name}${d}@${host}${d}] [${green}$(collapse_pwd)${d}]$(git_prompt_info) ${${KEYMAP/vicmd/$ZSH_VIM_PROMPT}/(main|viins)/}
${d}$(virtualenv_info)$(prompt_char) ${white}'
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

setopt prompt_subst

if [ ${+KEYCHAIN} = 1 ]; then
    precmd() { keychain --agents ssh,gpg --quiet --timeout 86400 id_rsa D71F663F }
fi
