# Prompt

autoload -U colors && colors

local d="%F{blue}"
local open="%F{blue}[%f"
local close="%F{blue}]%f"
local at="%F{blue}@%f"
local name="%F{red}%n%f"
local host="%F{yellow}%m%f"
local tstamp="%F{cyan}%*%f"

ZSH_VIM_PROMPT="${open}%F{green}NORMAL%f${close}"

function collapse_pwd {
    echo -n "%F{green}"
    echo -n $(pwd | sed -e "s,^$HOME,~,")
    echo -n "%f"
}

function prompt_char {
    git branch >/dev/null 2>/dev/null && echo '±' && return
    hg root >/dev/null 2>/dev/null && echo '☿' && return
    echo '○'
}

# Determine active Python virtualenv details.
function virtualenv_info () {
  PYENV_VERSION="`pyenv version-name`"
  PYENV_SHORT="${PYENV_VERSION%%/*}"
  PYENV_VENV="${PYENV_VERSION##*/}"
  if [[ "$PYENV_VERSION" == "system" ]] ; then
      echo ""
  elif [[ "$PYENV_VENV" == "$PYENV_VERSION" ]] ; then
      echo "${open}%F{magenta}${PYENV_VERSION}%f${close} "
  else
      echo "${open}%F{magenta}${PYENV_SHORT}/${PYENV_VENV}%f${close} "
  fi
}

# Set it initially
PROMPT='
${open}${name}${at}${host}${close} ${open}$(collapse_pwd)${close}$(git_prompt_info) ${${KEYMAP/vicmd/$ZSH_VIM_PROMPT}/(main|viins)/}
$(virtualenv_info)%F{blue}$(prompt_char)%f '

function zle-line-init zle-keymap-select {
    PROMPT='
${open}${tstamp}${close} ${open}${name}${at}${host}${close} ${open}$(collapse_pwd)${close}$(git_prompt_info) ${${KEYMAP/vicmd/$ZSH_VIM_PROMPT}/(main|viins)/}
$(virtualenv_info)%F{blue}$(prompt_char)%f '
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

setopt prompt_subst

if [ ${+KEYCHAIN} = 1 ]; then
    precmd() { keychain --agents ssh,gpg --quiet --timeout 86400 id_rsa D71F663F }
fi
